import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/network.model/model/Model.ecore"

//
// Virtual networks
//

// Match from complete virtual network to a substrate server
rule vnetToServer() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateNode: SubstrateServer {
		.residualCpu := substrateNode.residualCpu - virtualNetwork.cpu
		.residualMemory := substrateNode.residualMemory - virtualNetwork.memory
		.residualStorage := substrateNode.residualStorage - virtualNetwork.storage
		++ -guestNetworks -> virtualNetwork
		// Embedding of the virtual elements is done by the "virtualNetwork" block below
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateNode
	}
	
	virtualNetwork: VirtualNetwork {
		++ -hostServer -> substrateNode
		// Embed all virtual elements onto the substrate server
		forEach links->l <VirtualLink> {
			++ substrateNode - guestLinks->l
		}
		forEach nodes->srv <VirtualServer> {
			++ substrateNode - guestServers->srv
		}
		forEach nodes->sw <VirtualSwitch> {
			++ substrateNode - guestSwitches->sw
		}
	}
	
	# virtualNetwork.cpu <= substrateNode.residualCpu
	# virtualNetwork.memory <= substrateNode.residualMemory
	# virtualNetwork.storage <= substrateNode.residualStorage
	
	// Explicitly exclude substrate servers with any residual resource equals to 0
	// TODO: Fix data type mismatch
	// See: https://github.com/eMoflon/emoflon-ibex/issues/408
//	# substrateNode.residualCpu > 0
//	# substrateNode.residualMemory > 0
//	# substrateNode.residualStorage > 0
}

