import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/network.model/model/Model.ecore"

//
// Networks
//

rule embedNetwork() {
	root : Root {
		-networks -> subNet
		-networks -> virtNet
	}
	
	subNet: SubstrateNetwork {
		++ -guests->virtNet
	}
	
	virtNet: VirtualNetwork
} when noIncompleteEmbedding

condition noIncompleteEmbedding = forbid incompleteEmbedding

condition isVNR = forbid embeddedNetwork

//
// Servers
//

// Substrate server must have at least the required resources available
rule serverMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateNode: SubstrateServer {
		.residualCpu := substrateNode.residualCpu - virtualNode.cpu
		.residualMemory := substrateNode.residualMemory - virtualNode.memory
		.residualStorage := substrateNode.residualStorage - virtualNode.storage
		++ -guestServers -> virtualNode
	}
	
	virtualNode: VirtualServer {
		++ -host -> substrateNode 
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateNode
	}
	
	virtualNetwork: VirtualNetwork {
		-nodes -> virtualNode
	}
	
	# virtualNode.cpu <= substrateNode.residualCpu
	# virtualNode.memory <= substrateNode.residualMemory
	# virtualNode.storage <= substrateNode.residualStorage
} when isVNR

// Two substrate servers have at least the required resources available
rule serverMatchPositiveBackup() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateNode
		-nodes -> substrateNodeBackup
	}
	
	virtualNetwork: VirtualNetwork {
		-nodes -> virtualNode
	}
	
	substrateNode: SubstrateServer {
		.residualCpu := substrateNode.residualCpu - virtualNode.cpu
		.residualMemory := substrateNode.residualMemory - virtualNode.memory
		.residualStorage := substrateNode.residualStorage - virtualNode.storage
	}
	
	substrateNodeBackup: SubstrateServer {
		.residualCpu := substrateNode.residualCpu - virtualNode.cpu
		.residualMemory := substrateNode.residualMemory - virtualNode.memory
		.residualStorage := substrateNode.residualStorage - virtualNode.storage
	}
	
	virtualNode: VirtualServer {
		++ -host -> substrateNode 
		++ -backup -> substrateNodeBackup
	}

	# virtualNode.cpu <= substrateNode.residualCpu
	# virtualNode.memory <= substrateNode.residualMemory
	# virtualNode.storage <= substrateNode.residualStorage
} when isVNR

//
// Switches
//

// Match from virtual switch to substrate node
// (Substrate node could either be a server or a switch.)
rule switchNodeMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateNode: SubstrateNode {
		++ -guestSwitches -> virtualSwitch
	}
	
	virtualSwitch : VirtualSwitch {
		++ -host -> substrateNode
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateNode
	}
	
	virtualNetwork: VirtualNetwork {
		-nodes -> virtualSwitch
	}
} when isVNR

//
// Links
//

// Match from a virtual link to a substrate path (positive)
rule linkPathMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substratePath: SubstratePath {
		++ -guestLinks -> virtualLink
		.residualBandwidth := substratePath.residualBandwidth - virtualLink.bandwidth
		
		// Update all substrate links
		forEach links->l {
			iterator::l.residualBandwidth := iterator::l.residualBandwidth - virtualLink.bandwidth
		}
	}
	
	virtualLink: VirtualLink {
		++ -host -> substratePath
	}
	
	substrateNetwork: SubstrateNetwork {
		-paths -> substratePath
	}
	
	virtualNetwork: VirtualNetwork {
		-links -> virtualLink
	}
	
	# virtualLink.bandwidth <= substratePath.residualBandwidth
	
	// Explicitly exclude substrate paths with a residual bandwidth equals to 0
	# substratePath.residualBandwidth > 0
} when isVNR

// Match from virtual link to substrate server
// This one does not have any constraints.
rule linkServerMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateServer: SubstrateServer {
		++ -guestLinks -> virtualLink
	}
	
	virtualLink: VirtualLink {
		++ -host -> substrateServer
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateServer
	}
	
	virtualNetwork: VirtualNetwork {
		-links -> virtualLink
	}
} when isVNR
