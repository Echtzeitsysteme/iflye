import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/network.model/model/Model.ecore"

//
// Servers
//

// Substrate server must have at least the required resources available
rule serverMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateNode: SubstrateServer {
		.residualCpu := substrateNode.residualCpu - virtualNode.cpu
		.residualMemory := substrateNode.residualMemory - virtualNode.memory
		.residualStorage := substrateNode.residualStorage - virtualNode.storage
		++ -guestServers -> virtualNode
	}
	
	virtualNode: VirtualServer {
		++ -host -> substrateNode
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateNode
	}
	
	virtualNetwork: VirtualNetwork {
		-nodes -> virtualNode
	}
	
	# virtualNode.cpu <= substrateNode.residualCpu
	# virtualNode.memory <= substrateNode.residualMemory
	# virtualNode.storage <= substrateNode.residualStorage
	
	// Explicitly exclude substrate servers with any residual resource equals to 0
	// TODO: Fix type incompatibility
	// See: https://github.com/eMoflon/emoflon-ibex/issues/408
//	# substrateNode.residualCpu > 0
//	# substrateNode.residualMemory > 0
//	# substrateNode.residualStorage > 0
}

//
// Switches
//

// Match from virtual switch to substrate switch
rule switchMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateSwitch: SubstrateSwitch {
		++ -guestSwitches -> virtualSwitch
	}
	
	virtualSwitch : VirtualSwitch {
		++ -host -> substrateSwitch
	}
	
	substrateNetwork: SubstrateNetwork {
		-nodes -> substrateSwitch
	}
	
	virtualNetwork: VirtualNetwork {
		-nodes -> virtualSwitch
	}
}

//
// Links
//

// Match from a virtual link to a substrate link
rule linkMatchPositive() {
	root: Root {
		-networks -> substrateNetwork
		-networks -> virtualNetwork
	}
	
	substrateLink: SubstrateLink {
		++ -guestLinks -> virtualLink
		.residualBandwidth := substrateLink.residualBandwidth - virtualLink.bandwidth
	}
	
	virtualLink: VirtualLink {
		++ -host -> substrateLink
	}
	
	substrateNetwork: SubstrateNetwork {
		-links -> substrateLink
	}
	
	virtualNetwork: VirtualNetwork {
		-links -> virtualLink
	}
	
	# virtualLink.bandwidth <= substrateLink.residualBandwidth
	
	// Explicitly exclude substrate paths with a residual bandwidth equals to 0
	# substrateLink.residualBandwidth > 0
}
